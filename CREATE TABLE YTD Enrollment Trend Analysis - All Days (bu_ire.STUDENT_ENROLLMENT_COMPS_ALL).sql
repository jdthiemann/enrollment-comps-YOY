/*	Drop existing table */
--DROP TABLE bu_ire.STUDENT_ENROLLMENT_COMPS_ALL;

/*	Delete data from table */
--DELETE FROM bu_ire.STUDENT_ENROLLMENT_COMPS_ALL;

/*	Recreate table with new definitions */
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE TABLE bu_ire.STUDENT_ENROLLMENT_COMPS_ALL
	(
		ENRCOMPS_ALL_ID INT IDENTITY(1,1) NOT NULL
		, TERM VARCHAR(7) NOT NULL
		, TERM_DESC VARCHAR(40) NOT NULL
		, TERM_SEASON VARCHAR(10) NOT NULL
		, TERM_RPT_YR INT NOT NULL
		, TERM_SEQ_NO TINYINT NOT NULL
		, TERM_SEQ VARCHAR(7) NOT NULL
		, COLLEAGUE_FLAG VARCHAR(1) NULL
		, TEST_DATE DATETIME NOT NULL
		, BATES_STAMP INT NOT NULL
		, BATES_DIST INT NOT NULL
		, BATES_TODAY INT NOT NULL
		, LATEST_TERM VARCHAR(7) NOT NULL
		, LATEST_TERM_DESC VARCHAR(40) NOT NULL
		, LAST_TERM1 VARCHAR(10) NULL
		, LAST_TERM2 VARCHAR(10) NULL
		, STUDENT_TERMS_ID VARCHAR(18) NULL
		, STTR_STUDENT VARCHAR(10) NULL
		, STTR_ACAD_LEVEL VARCHAR(2) NULL
		, STUDENT_TYPE VARCHAR(40) NULL
		, ENR_TYPE INT NULL
		, STUDENT_POPULATION VARCHAR(50) NULL
		, COHORT VARCHAR(20) NULL
		, MATRIC_TERM VARCHAR(7) NULL
		, MATRIC_TERM_SEASON VARCHAR(10) NULL
		, MATRIC_TERM_RPT_YR INT NULL
		, MATRIC_TERM_SEQ_NO TINYINT NULL
		, STAT_RANK TINYINT NULL
		, STTR_STATUS VARCHAR(2) NULL
		, STC_CRED_SUM DEC(8,5) NULL
		, STC_CRED_SUM_NO_OFFSET DEC(8,5) NULL
		, UPDATE_DATE DATETIME NOT NULL
		, CONSTRAINT PK_ENRCOMPS_ALL_ID PRIMARY KEY (ENRCOMPS_ALL_ID ASC)
		WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
	) ON [PRIMARY]
GO

/*	Create table indices */
CREATE INDEX IDX_TERM ON bu_ire.STUDENT_ENROLLMENT_COMPS_ALL (TERM);
CREATE INDEX IDX_TERM_SEQ ON bu_ire.STUDENT_ENROLLMENT_COMPS_ALL (TERM_SEQ);
CREATE INDEX IDX_BATES_STAMP ON bu_ire.STUDENT_ENROLLMENT_COMPS_ALL (BATES_STAMP);
CREATE INDEX IDX_STTR_STUDENT ON bu_ire.STUDENT_ENROLLMENT_COMPS_ALL (STTR_STUDENT);


/*	Rebuild data in table */
;WITH PIVOT_CTE (TERM, TERM_SEASON, TERM_START_DATE, TERM_END_DATE, PIVOT_DATE) AS
	(
		SELECT TERMS_ID, TERM_SEASON, TERM_START_DATE, TERM_END_DATE, PIVOT_DATE
		FROM
			(
				SELECT
					T.TERMS_ID
					, CASE RIGHT(T.TERMS_ID,2)
							WHEN 'FA' THEN 'Fall'
							WHEN 'SP' THEN 'Spring'
							WHEN 'SU' THEN 'Summer'
						END AS TERM_SEASON
					, T.TERM_START_DATE
					, T.TERM_END_DATE
					, DATEDIFF(DAY, T.B13_TERM_CENSUS_DATE, GETDATE()) AS PIVOT_DATE
					, ROW_NUMBER() OVER(PARTITION BY
							CASE RIGHT(T.TERMS_ID,2)
							WHEN 'FA' THEN 'Fall'
							WHEN 'SP' THEN 'Spring'
							WHEN 'SU' THEN 'Summer'
						END
							ORDER BY ABS(DATEDIFF(DAY, T.B13_TERM_CENSUS_DATE, GETDATE()))) AS DAY_DIF

				FROM
					ODS_TERMS T

				WHERE
					RIGHT(T.TERMS_ID,2) IN ('FA', 'SP', 'SU')
					AND GETDATE() <= DATEADD(DAY, 11, T.TERM_END_DATE)
					AND GETDATE() >= T.B13_TERM_REG_START_DATE
			) X
		WHERE X.DAY_DIF = 1
	)

, BATES_RANGE (TERM, TERM_DESC, TERM_SEASON, BATES_STAMP) AS
	(
		SELECT TERM, TERM_DESC, TERM_SEASON, BATES_STAMP+1 AS BATES_STAMP
		FROM bu_ire.STUDENT_ENROLLMENT_YTD Y
		WHERE CONVERT(DATE,TEST_DATE) = CONVERT(DATE,DATEADD(D,-1,GETDATE()))
		GROUP BY TERM, TERM_DESC, TERM_SEASON, BATES_STAMP
	)

/* remove comment from line below to copy & paste into SQL Server Agent */
INSERT INTO bu_ire.STUDENT_ENROLLMENT_COMPS_ALL

SELECT
	Y.TERM
	, Y.TERM_DESC
	, Y.TERM_SEASON
	, Y.TERM_RPT_YR
	, Y.TERM_SEQ_NO
	, Y.TERM_SEQ
	, Y.COLLEAGUE_FLAG
	, DATEADD(DAY, -1, Y.TEST_DATE) AS TEST_DATE
	, Y.BATES_STAMP
	, Y.BATES_STAMP-B.BATES_STAMP AS BATES_DIST
	, B.BATES_STAMP AS BATES_TODAY
	, B.TERM AS LATEST_TERM
	, B.TERM_DESC AS LATEST_TERM_DESC
	, Y.LAST_TERM1
	, Y.LAST_TERM2
	, Y.STUDENT_TERMS_ID
	, Y.STTR_STUDENT
	, Y.STTR_ACAD_LEVEL
	, Y.STUDENT_TYPE
	, Y.ENR_TYPE
	, Y.STUDENT_POPULATION
	, Y.COHORT
	, Y.MATRIC_TERM
	, Y.MATRIC_TERM_SEASON
	, Y.MATRIC_TERM_RPT_YR
	, Y.MATRIC_TERM_SEQ_NO
	, Y.STAT_RANK
	, Y.STTR_STATUS
	, Y.STC_CRED_SUM
	, Y.STC_CRED_SUM_NO_OFFSET
	, GETDATE() AS UPDATE_DATE

FROM
	PIVOT_CTE T
	LEFT OUTER JOIN bu_ire.STUDENT_ENROLLMENT_YTD Y ON T.TERM_SEASON=Y.TERM_SEASON
	LEFT OUTER JOIN BATES_RANGE B ON Y.TERM_SEASON=B.TERM_SEASON

ORDER BY 6, 10, 9

/*	Confirm data loaded correctly */
SELECT *
FROM bu_ire.STUDENT_ENROLLMENT_COMPS_ALL
WHERE TERM_SEASON = 'Fall'
AND STTR_STUDENT = 1254935
ORDER BY 6, 10, 9

/*	Revision history:
		JDT 7/29/2020: Added the "NO OFFSET" version of the credit summary for a given day.
		JDT 6/8/2020: Added Bates distance fields to incorporate a dynamic date range and total comparisons into CROA report query filters.
		JDT 4/13/2020: Added last term 1 and 2 for use in COALESCE statements to make sure all students have academic program history
				fields available for input controls, even if they ultimately withdrew before census date in the subject term.
		JDT 12/2/2019: Updated column definitions to align with other files used in the student enrollment comparisons project.
		JDT 7/31/2019: Created view as a fork of the original bu_ire.STUDENT_ENROLLMENT_COMPS view.
*/
